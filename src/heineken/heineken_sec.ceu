#include "media/media.ceu"

var int width = 1024;
var int height = 576;

input (none) SHOW_OPTIONS;
input (none) KILL_APP;

output (none) ACCEPT;
output (int) CHOICE;

var[] byte init = []  .. "resources/priv/app01_tela_v2.png";
var Properties.Image initprop = val Properties.Image (
    Region (0, 0, width, height, 2),
    1.0);

var&? Scene s = spawn Scene (Size(width, height));
watching s, KILL_APP do
  par/or do
    await Play (&init, &initprop, &s.handle);
  with
    var _char_ptr_ext key;
    (_, key, _) = await CM_SCENE_KEY until _strcmp (key, "F1") == 0;
    emit ACCEPT ();
  end

  watching SHOW_OPTIONS do
    var[] byte background = [] .. "resources/priv/app_espera.png";

    var Properties.Image prop = val Properties.Image (
                    Region (0, 0, width, height, 2), 1.0);
    await Play (&background, &prop, &s.handle);
  end

  var[] byte question = [] .. "resources/priv/app02_tela_v2.png";
  var[] byte op1 = [] .. "resources/priv/app02_option1_v2.png";
  var[] byte op2 = [] .. "resources/priv/app02_option2_v2.png";
  var[] byte op3 = [] .. "resources/priv/app02_option3_v2.png";

  var Properties.Image questionprop = val Properties.Image (
                    Region (0, 0, width, height, 1), 1.0);
  var Properties.Image op1prop = val Properties.Image (
                    Region (0, 270, width, 55, 2), 1.0);
  var Properties.Image op2prop = val Properties.Image (
                    Region (0, 330, width, 55, 2), 1.0);
  var Properties.Image op3prop = val Properties.Image (
                    Region (0, 390, width, 55, 2), 1.0);

  event (none) go_to_end;
  watching go_to_end
  do
    par do
      await Play (&question, &questionprop, &s.handle);
    with
      await Play (&op1, &op1prop, &s.handle);
    with                        
      await Play (&op2, &op2prop, &s.handle);
    with                        
      await Play (&op3, &op3prop, &s.handle);
    with
      loop do
        var _char_ptr_ext key;
        (_, key, _) = await CM_SCENE_KEY;
        if _strcmp (key, "F1") == 0 then
          emit CHOICE (1);
          emit go_to_end;
          break;
        else/if _strcmp (key, "F2") == 0 then
          emit CHOICE (2);
          emit go_to_end;
          break;
        else/if _strcmp (key, "F3") == 0 then
          emit CHOICE (3);
          emit go_to_end;
          break;
        end
      end
    end
  end
  var[] byte final_img = [] .. "resources/priv/app03_tela_v2.png";
  var Properties.Image prop = val Properties.Image (
                    Region (0, 0, width, height, 2), 1.0);
  await Play (&final_img, &prop, &s.handle);
end
