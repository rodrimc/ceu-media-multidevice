#include "media/media.ceu"

/*
 * Mapping: scripts/tv_mappings_v2.lua
 */

var[] byte videouri = [] .. "resources/video-frame.avi";
var Properties.Video videoprop = val Properties.Video (
                Region (0, 0, 720, 480, 1),
                1.0, 1.0);

#define SECONDS * 1000000000

input (none) PLAY;
input (none) PAUSE;
input (none) SEEK_FORWARD;
input (none) SEEK_BACKWARD;
input (none) VOLUME_UP;
input (none) VOLUME_DOWN;
input (none) TURN_ON;
input (none) TURN_OFF;

loop do
  await TURN_ON;
  watching TURN_OFF do
    var Size size = val Size (720, 480);
    var&? Scene scene = spawn Scene(size);
    watching scene do
      var&? Play p = spawn Play (&videouri, &videoprop, &scene.handle);

      event (r64) set_volume;
      watching p do
        pool [] Scene_Pause  pause_pool;
        pool [] Scene_Resume resume_pool;
        par do
          every PLAY do
            spawn Scene_Resume (&scene.handle) in resume_pool;
          end
        with
          every PAUSE do
            spawn Scene_Pause (&scene.handle) in pause_pool;
          end
        with
          every SEEK_FORWARD do
            call Player_Seek (&p.handle, 10 SECONDS , true);
          end
        with
          every SEEK_BACKWARD do
            call Player_Seek (&p.handle, -10 SECONDS, true);
          end
        with
          every VOLUME_UP do
            var r64 vol_level = call Player_Get_Double (&p.handle, "volume");
            if vol_level < 1.0 then
              vol_level = vol_level + 0.2;
            end
            emit set_volume (vol_level);
          end
        with
          every VOLUME_DOWN do
            var r64 vol_level = call Player_Get_Double (&p.handle, "volume");
            if vol_level > 0.0 then
              vol_level = vol_level - 0.2;
            end
            emit set_volume (vol_level);
          end
        with
          event (none) draw;
          par do
            loop do
              await draw;
              watching set_volume, 2s do
                var int vol_level = (call Player_Get_Double (&p.handle, "volume") * 10) as int;

                var[] byte vol_bar = _;
                if vol_level == 0 then
                  vol_bar = [] .. "resources/vol_0.png";
                else/if vol_level == 2 then
                  vol_bar = [] .. "resources/vol_1.png";
                else/if vol_level == 4 then
                  vol_bar = [] .. "resources/vol_2.png";
                else/if vol_level == 6 then
                  vol_bar = [] .. "resources/vol_3.png";
                else/if vol_level == 8 then
                  vol_bar = [] .. "resources/vol_4.png";
                else/if vol_level == 10 then
                  vol_bar = [] .. "resources/vol_5.png";
                end

                var Properties.Image volumeprop = val Properties.Image (
                    Region (0, 0, 232, 119, 2), 1.0);

                await Play (&vol_bar, &volumeprop, &scene.handle);
              end
            end
          with
            var r64 vol_level = _;
            every vol_level in set_volume do
              call Player_Set_Double (&p.handle, "volume", vol_level);
              emit draw;
            end
          end
        end
      end
    end
  end
end
