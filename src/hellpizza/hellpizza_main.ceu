#include "media/media.ceu"

input (int) USER_CHOICE;
output (none) INTERACTIVITY;

var int width = 1024;
var int height = 576;

var[] byte main_video = [] .. "resources/priv2/vid.mp4";
var Properties.Video videoprop = val Properties.Video (
                          Region (0, 0, width, height, 1),
                          1.0, 0.5);

var&? Scene s = spawn Scene (Size (width,height));

watching s
do
  var int choice = 1;
  par/or do
    await Play (&main_video, &videoprop, &s.handle);
  with
    await 35s;
    emit INTERACTIVITY ();
    choice = await USER_CHOICE;
  end

  var[] byte path;
  if choice == 1 then
    path = [] .. "resources/priv2/let_him_in.mp4";
  else
    path = [] .. "resources/priv2/leave_him_behind.mp4";
  end

  var Properties.Video prop = val Properties.Video (
                                  Region (0, 0, width, height, 1),
                                  1.0, 1.0);
  watching 10s
  do
    await Play (&path, &prop, &s.handle);
  end;
end

escape 0;

