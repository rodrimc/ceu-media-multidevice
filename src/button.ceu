#include "mars/mars-media.ceu"

var Size size = val Size (720, 480);
var Size b_size = val Size (100, 100);

var Media.Video video = val Media.Video (
                [] .. "resources/bunny.ogg",
                Region (0, 0, size.width, size.height, 1),
                1.0,
                1.0);

var Media.Text text = val Media.Text (
                [] .. "", [] .. "sans 20",
                Region(0, 350, size.width, 100, 2),
                0xffffffff);

var Media.Image on_button = val Media.Image (
                [] .. "resources/on.png",
                Region (size.width/2 - b_size.width/2,
                        size.height/2 - b_size.height/2,
                        b_size.width, b_size.height, 2),
                1.0);

var Media.Image off_button = val Media.Image (
                [] .. "resources/off.png",
                Region (size.width/2 - b_size.width/2,
                        size.height/2 - b_size.height/2,
                        b_size.width, b_size.height, 2),
                1.0);

var&? Scene scene = spawn Scene(size);
watching scene do
  spawn Play (&scene.scene, &video);
  var&? Play p = spawn Play(&scene.scene, &text);
  watching  p do
    par/or do
      var[] byte str;
      await 5s;
      var&? Play p_button = spawn Play (&scene.scene, &on_button);
      watching p_button, 10s do
        var uint obj;
        var bool press = _;
        var uint id = _;

        (obj, _, _, _, press) = await CM_PLAYER_MOUSE_CLICK
                                until (press and obj == p_button.player.self);

        var uint device = call Get_Event_Device_Id ();
        str = [] .. "Winner: Player " ..  [[ tostring (@device) ]] .. ". ";
        if (device == call Get_Device_Id()) then
          str = str .. "You won!";
        else
          str = str .. "You lost!";
        end

        call Player_Set_Char (&p.player, "text", &&str[0]);
        await Play (&scene.scene, &off_button);
      end
    with
      var uint obj;
      var _char_ptr_ext key;
      (obj, key, _) = await CM_SCENE_KEY until (_strcmp(key, "q") == 0);
    end
  end
end

escape 0;
