#include "multidevice.ceu"
#include "media/media.ceu"

var Size size = val Size (720, 480);  

var Media.Video video = val Media.Video (
                [] .. "resources/ChairDivXL.avi",
                Region (0, 0, 0, size.width, size.height), 
                1.0,
                0);

var Media.Text text = val Media.Text (
                [] .. "", [] .. "sans 40", 
                Region(0, 350, 1, size.width, 100),
                0xffffffff);

var& IScene scene;
watching Scene(size) -> (scene) do
  var& IPlay p;
  var& IMediaClient client; 
  var& int myid;
  watching Play(&scene, &text) -> (p),
           MediaClient (LOCALHOST, 8888, 0) -> (client, myid)
  do
    await client.joined;

    par/or do
      spawn Play (&scene, &video);
      await FOREVER;
    with
      var bool pressed;
      every (_, _, _, pressed) in scene.mouse_clicked do
        if pressed then 
          emit client.send_click;
        end
      end
    with
      var int i = _;
      every i in client.clicked do
        vector[] byte str;
        str = [] .. [[ tostring (@i) ]]; 
        call Play_Set_Char (&p, "text", &&str[0]);
      end
    with
      var bool press;
      var _char_ptr_ext key;
      (key, press) = await scene.key until (_strcmp(key, "q") == 0);
    end
  end
end

escape 0;
