#include "mars/mars-media.ceu"

var Size size = val Size (720, 480);
var Size b_size = val Size (100, 100);

var Media.Video video = val Media.Video (
                [] .. "resources/bunny.ogg",
                Region (0, 0, size.width, size.height, 1),
                1.0,
                1.0);

var Media.Text text = val Media.Text (
                [] .. "", [] .. "sans 20",
                Region(0, 350, size.width, 100, 2),
                0xffffffff);

var Media.Image on_button = val Media.Image (
                [] .. "resources/on.png",
                Region (size.width/2 - b_size.width/2,
                        size.height/2 - b_size.height/2,
                        b_size.width, b_size.height, 2),
                1.0);

var Media.Image off_button = val Media.Image (
                [] .. "resources/off.png",
                Region (size.width/2 - b_size.width/2,
                        size.height/2 - b_size.height/2,
                        b_size.width, b_size.height, 2),
                1.0);

var& IScene scene;
watching Scene(size) -> (&scene)
do
  spawn Play (&scene, &video);
  var& IPlayer p;
  watching Play(&scene, &text) -> (&p)
  do
    par/or do
      vector[] byte str;
      await 5s;
      var& IPlayer p_button;
      watching Play (&scene, &on_button) -> (&p_button), 10s
      do
        var uint device;
        var uint obj;
        var bool press = _;
        var uint id = _;

        (device, obj, _, _, _, press) = await CM_PLAYER_MOUSE_CLICK
                                        until (press and obj == p_button.self);
        str = [] .. "Winner: Player " ..  [[ tostring (@device) ]] .. ". ";
        if (device == device_id) then
          str = str .. "You won!";
        else
          str = str .. "You lost!";
        end

        call Player_Set_Char (&p, "text", &&str[0]);
        await Play (&scene, &off_button);
      end
    with
      var uint device;
      var uint obj;
      var _char_ptr_ext key;
      (device, obj, key, _) = await CM_SCENE_KEY until (_strcmp(key, "q") == 0);
    end
  end
end

escape 0;
