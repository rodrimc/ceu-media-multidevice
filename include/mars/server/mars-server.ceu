#ifndef _CEU_MARS_SERVER
#define _CEU_MARS_SERVER

/* Server Lua global */
[[
  SERVER =
    {
      naming = nil,
      roles = {}
    }
]]

#include "mars/util/mars-defs.ceu"

native/pos do
	tceu_callback_ret evt_cb (int cmd,
														tceu_callback_arg p1,
														tceu_callback_arg p2,
														const char* filename,
														u32 line)
	{
    static int flag = 0;
    tceu_callback_ret ret = { .is_handled=0 };
    if (flag == 0 && cmd == CEU_CALLBACK_STEP)
    {
      if (CEU_APP.argc > 1)
      {
        char *str = strdup (CEU_APP.argv[1]);
        ceu_input (CEU_INPUT_INIT, &str);
        free (str);
      }

      flag = 1;
      ret.is_handled = 1;
    }
    return ret;
  }

  tceu_callback cb = { &evt_cb, NULL };
end

native/nohold
  _ceu_callback_register,
  _cb,
;

_ceu_callback_register (&&_cb);

input (_char&&) INIT;

spawn do
  var _char&& script = await INIT;
  var Exception? e;
  catch e do
    _printf ("[server] init script: %s\n", script);
    [[
      dofile (@script)
    ]]
  end
  if e? then
    _fprintf (_stderr, "[INIT error:] %s\n", e!.message);
  end
end

data UV_TCP_Server_Data with
  var bool isReading;
end

/*** SERVER SIDE ***/
event (none) send_join_reply;
event (uint) send_broadcast_join;
event (uint, int) send_broadcast_message;
event (uint) send_tick_message;

var []byte cur_payload;
var []byte cur_role;

var uint id_ref = 1;
var usize peers_count = 0;

#define FREQ 50
spawn do
  every (FREQ)ms do
    emit send_tick_message(FREQ);
  end
end

code/await Send_Join_Reply (var uint handle_id,
                            var& UV_Stream stub,
                            var uint id,
                            var& []byte role,
                            var int index) -> none
do
  var Exception.Lua? e;
  catch e do
    [[
      luabuff = 'type='  .. tostring(@S_JOINED)          .. ',' ..
                'id='    .. tostring(@id)                .. ',' ..
                'peers=' .. tostring(@outer.peers_count) .. ',' ..
                'role="' .. tostring(@role)              .. '",' ..
                'index=' .. tostring(@index)
    ]]
    var[] byte buff = [] .. [[ luabuff ]];
    await Send (handle_id, &stub, &buff);
  end
  if e? then
    _fprintf (_stderr, "[Send_Join_Reply error]: %s\n", e!.message);
  end
end

code/await Send_Broadcast_Join (var uint handle_id,
                                var& UV_Stream stream,
                                var uint id) -> none
do
  var Exception.Lua? e;
  catch e do
    var u64 ts = _now ();
    [[
      luabuff = 'type='      .. tostring(@S_NEW_PEER)        .. ',' ..
                'new_peer='  .. tostring(@id)                .. ',' ..
                'peers='     .. tostring(@outer.peers_count)
    ]]
    var[] byte buff = [] .. [[ luabuff ]];
    await Send (handle_id, &stream, &buff);
  end
  if e? then
    _fprintf (_stderr, "[Send_Broadcast_Join error]: %s\n", e!.message);
  end
end

code/await Send_Broadcast_Message (var uint handle_id,
                                   var& UV_Stream stream,
                                   var uint from,
                                   var& []byte role,
                                   var int index,
                                   var& []byte payload) -> none
do
  var Exception.Lua? e;
  catch e do
    var u64 ts = _now ();
    [[
      luabuff = 'type='      .. tostring(@S_BROADCAST)       .. ',' ..
                'from='      .. tostring(@from)              .. ',' ..
                'payload='   .. tostring(@payload)           .. ',' ..
                'role="'     .. tostring(@role)              .. '",' ..
                'index='     .. tostring(@index)             .. ',' ..
                'peers='     .. tostring(@outer.peers_count)
    ]]
    var[] byte buff = [] .. [[ luabuff ]];
    await Send (handle_id, &stream, &buff);
  end
  if e? then
    _fprintf (_stderr, "[Send_Broadcast_Message error]: %s\n", e!.message);
  end
end

code/await Send_Tick_Message (var uint handle_id,
                              var& UV_Stream stream,
                              var uint freq) -> none
do
  var Exception.Lua? e;
  catch e do
    var[] byte buff = _;
      [[
        luabuff = 'type=' .. tostring(@S_TICK)               .. ',' ..
                  'freq=' .. tostring(@freq)
      ]]

      buff = [] .. [[ luabuff ]];
    await Send (handle_id, &stream, &buff);
  end

  if e? then
      _fprintf(_stderr, "[Send_Tick_Message error]: %s\n", e!.message);
  end
end

code/await Reading_Handler (var& UV_Stream stream) -> NEVER
do
  loop do
    var[] byte buffer;
    var bool success = await Receive (&stream, &buffer);

    if not success then
      continue;
    end
    _printf ("received: %s\n", &&buffer[0]);

    var Exception.Lua? e;
    catch e do
      [[
        TABLE = load ('return{' .. @buffer .. '}')()
      ]]

      var int type = [[ TABLE.type ]];

      if  type == P_REGULAR_SEND then
        var uint from = [[ TABLE.from ]];
        var int index = [[ TABLE.index or -1 ]];
        var [] byte payload = [] .. [[ serialize(TABLE.payload) ]];
        outer.cur_role = [] .. [[ TABLE.role ]];

        outer.cur_payload = [] .. payload;
        emit outer.send_broadcast_message (from, index);
      end
    end
    if e? then
      _printf ("[error]: %s\n", e!.message);
    end
  end
end

code/await Writing_Handler (var& UV_Stream stream) -> NEVER
                            throws Exception.Uv
do
  var uint handle_id = outer.id_ref;
  outer.id_ref = outer.id_ref + 1;

  outer.peers_count = outer.peers_count + 1;
  do finalize with
    outer.peers_count = outer.peers_count - 1;
  end

  pool[] Send_Join_Reply        pool_join_reply;
  pool[] Send_Broadcast_Join    pool_broadcast_join;
  pool[] Send_Broadcast_Message pool_broadcast_message;
  pool[] Send_Tick_Message      pool_tick_message;

  var[] byte role = [] .. "";
  par do
    var uint freq;
    every freq in outer.send_tick_message do
      spawn Send_Tick_Message (handle_id, &stream, 50) in pool_tick_message;
    end
  with
    loop do
      var[] byte buff;
      var bool success = await Receive (&stream, &buff);

      var Exception.Lua? e;
      catch e do
        [[ MESSAGE = load ('return {' .. @buff .. '}')() ]]
        var int type = [[ MESSAGE.type ]];
        var int index = _;

        if (type == P_JOIN) then
          [[
            if SERVER.naming and type(SERVER.naming) == 'function' then
              role = SERVER.naming (@handle_id)
              role = role or ''

              if SERVER.roles[role] == nil then
                SERVER.roles[role] = 1
              else
                SERVER.roles[role] = SERVER.roles[role] + 1
              end

              index = SERVER.roles[role]
            else
              role = ''
              index = 0
            end
          ]]

          role = [] .. [[ role ]];
          index = [[ index ]];

          spawn Send_Join_Reply (handle_id, &stream, handle_id, &role, index)
                                                      in pool_join_reply;
          emit outer.send_broadcast_join (handle_id);
          break;
        end
      end
      if e? then
        _fprintf (_stderr, "[error]: %s\n", e!.message);
      end
    end
  with
    var uint new_id;
    every new_id in outer.send_broadcast_join do
      spawn Send_Broadcast_Join (handle_id, &stream, new_id)
                                                      in pool_broadcast_join;
    end
  with
    var uint from;
    var int index;
    every (from, index) in outer.send_broadcast_message do
      spawn Send_Broadcast_Message (handle_id, &stream, from, &outer.cur_role,
                                    index, &outer.cur_payload)
                                    in pool_broadcast_message;
    end
  end
end

/* Handler for each client */
code/await UV_TCP_Server_Handler (var& UV_Stream stream,
                                  var& UV_TCP_Server_Data shared) -> int
do
  var Exception.Uv? e;
  catch e do
    if (shared.isReading) then
      await Reading_Handler (&stream);
    else
      await Writing_Handler (&stream);
    end
  end

  if e? then
    _fprintf(_stderr, "[error]: %s\n", e!.message);
  end
end

/* We must include tcp-server after we've defined UV_TCP_Server_Handler */
#include "uv/tcp-server.ceu"

#endif
