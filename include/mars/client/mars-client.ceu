#ifndef _CEU_MARS_CLIENT
#define _CEU_MARS_CLIENT

/* Client Lua global */
[[
  dofile ('build/mars-util.lua')
  dofile ('build/mars-client.lua')

  require 'build/peers'
]]

native
  _mars_now,
;

var u64 start_time = 0;
code/tight Get_Running_Time (none) -> u64
do
  escape _mars_now() - outer.start_time;
end


#include "mars/util/mars-defs.ceu"
#include "mars/client/mars-events.ceu"

event (none) abort;
event (uint) cancel_lag_timer;

/*** CLIENT API ***/

var[] byte server_ip = _;
var u16 wport = _;
var u16 rport = _;

code/await Client_Send_Message (var& Stub stub,
                                var&[] byte buffer) -> none
do
  var Exception.Lua? e;
  catch e do
    [[
      luabuff = 'type='       .. tostring(P_REGULAR_SEND) .. ',' ..
                'from='       .. tostring(@stub.id)       .. ',' ..
                'payload='    .. tostring(@buffer)
    ]]

    stub.wbuffer = [] .. [[ luabuff ]];
    emit stub.send_message;
  end
  if e? then
    _fprintf (_stderr, "[Client_Send_Message error]: %s\n", e!.message);
  end
end

code/await Handle_Message (var& Stub stub) -> none
do
  var Exception.Lua? e;
  catch e do

    var int type = _;

    /* parse message */
    [[ TABLE =  load ('return{' .. @stub.rbuffer .. '}')() ]]
    type = [[ TABLE.type ]];

#ifdef MARS_DEBUG
    if stub.ok then
      [[
         LOG = LOG or {}
         table.insert (LOG, @type)
      ]]
    end
#endif

    if type == S_ACCEPTED then
      stub.id = [[ TABLE.id ]];
      var uint peers = [[ TABLE.peers ]];

      [[
        if MARS.peer == nil then
          MARS.peer = Peer:new {
            id = TABLE.id,
            ip = @outer.server_ip,
            port = @outer.rport
          }
          for _,i in ipairs(MARS.instance) do
            MARS.peer:addInterface(i, MARS.interfaces[i])
          end
        end
      ]]

      emit stub.accepted (stub.id, peers);

    else/if type == S_NOT_ACCEPTED then
      _fprintf (_stderr, "[Client:] server didn't accepted me!\n");
      emit outer.abort;

    else/if type == S_NEW_PEER then
      var uint new_peer = [[ TABLE.peers ]];
      var uint peers = [[ TABLE.peers ]];

      emit stub.new_peer (new_peer, peers);
    else/if type == S_BROADCAST then
      [[ payload = 'return' .. serialize(TABLE.payload) ]]
      var u64 delay = [[ TABLE.delay ]];
      var uint seq = [[ TABLE.seq ]];
      var u64 now = call Get_Running_Time ();

      var u64 lag = _;

      if delay > now then
        lag =  delay - now;
      else
        lag = 0;
      end

      _printf ("***** NEW ACTION *****\n");
      _printf ("now: %lu\n", now);
      _printf ("delay: %lu\n", delay);
      _printf ("delay - now: %lu\n", lag);

      if lag > 0 then
        _printf ("(%u) sleeping %lu (us)\n", seq, delay - now);
        await (delay - now) us;
      end

      _printf ("(%u) awake: %lu\n", seq, call Get_Running_Time ());
      _printf ("***** END NEW ACTION *****\n");

      emit outer.cancel_lag_timer (seq);

      stub.message = [] .. [[ payload ]];

      emit stub.incoming_message;

      $stub.message = 0;
    else/if type == S_TICK_REQUEST then
      var u64 now = call Get_Running_Time ();
      var uint seq =  [[ TABLE.seq ]];

      [[
        luabuff = 'type=' .. tostring(P_TICK_RESPONSE) .. ',' ..
                  'from=' .. tostring(@stub.id)        .. ',' ..
                  'seq='  .. tostring(@seq)            .. ',' ..
                  'tick=' .. tostring(@now)
      ]]

      stub.wbuffer = [] .. [[ luabuff ]];

      _printf ("***** BEGIN TICK REQUEST *****\n");
      _printf ("now: %lu\n", now);
      _printf ("MAX LAG: %d\n", MAX_LAG);
      _printf ("now + MAX LAG: %lu\n", now + MAX_LAG);
      _printf ("***** END TICK REQUEST *****\n");

      par/and do
        emit stub.send_message;
      with
        par/or do
          var uint num;
          num = await outer.cancel_lag_timer until (num == seq);
          _printf ("timer canceled (%u)\n", seq);
        with
          await (MAX_LAG) ms;
          _printf ("timer expired (%u)\n", seq);
        end
      end
    else
      _printf ("[Client (%d) Unknow message type.\n", stub.id);
    end
  end
  if e? then
    _fprintf (_stderr, "[Handle_Message error]: %s\n", e!.message);
  end
end

code/await Client (var _char&& ip, var int session)
                          -> (var& Stub stub)
                            -> none
                  throws Exception.Uv
do
  var&? UV_TCP_Connect r_conn = spawn UV_TCP_Connect(ip, W_PORT, _);
  var&? UV_TCP_Connect w_conn = spawn UV_TCP_Connect(ip, R_PORT, _);

  event (none) stream_ready;

  var[] byte rbuffer;
  var[] byte wbuffer;
  var[] byte message;

#ifdef MARS_LOG
  do finalize with
    [[
       filename = os.tmpname()

       print ('logging to: ' .. filename)
       debugfile = io.open (filename, 'w+')

       last = nil
       count = nil
       for _,v in ipairs (LOG) do
         if not last then
           last = v
           count = 1
         elseif last == v then
           count = count + 1
         else
           debugfile:write (count, ' ' , last, '\n')
           last = v
           count = 1
         end
       end
       debugfile:write (count, ' ', last, '\n')
       debugfile:close()
    ]]
  end
#endif

  watching r_conn, w_conn, outer.abort
  do
    var Stub ref_ = val Stub (0, &wbuffer, &rbuffer, &message, false,  _,  _,
      _, _, _);
    stub = &ref_;

    par do
      /* read trail */
      par/and do
        pool [] Handle_Message pool_handle_message;
        loop do
          var[] byte buff;
          var bool success = await Receive (&r_conn.stream, &buff);
          if success then
            stub.rbuffer = [] .. buff;
            spawn Handle_Message (&stub) in pool_handle_message;
          end
        end
      with
        await r_conn.ok;
        emit stream_ready;
        outer.rport = await Get_Net_Address (&r_conn.stream, &outer.server_ip);
      end
    with
      /* write trail */
      par/and do
        pool [] Send pool_send;
        every stub.send_message do
          spawn Send (stub.id, &w_conn.stream, &stub.wbuffer) in pool_send;
        end
      with
        await w_conn.ok;
        emit stream_ready;
        outer.wport = await Get_Net_Address (&w_conn.stream, &outer.server_ip);
      end
    with
      var usize i;
      loop i in [1 -> 2] do
        await stream_ready;
      end
      emit stub.all_set_up;

      var Exception? e;
      catch e do
        [[
          luabuff = 'type=' .. tostring(P_JOIN) .. ',' ..
                    'interfaces="' .. MARS.instance[1] .. '"'
        ]]

        stub.wbuffer = [] .. [[ luabuff ]];

        /* send P_JOIN message */
        await Send (stub.id, &r_conn.stream, &stub.wbuffer);
      end

      if e? then
        _fprintf (_stderr, "[error]: %s\n", e!.message);
      end
    end
  end
end

#endif
