#include "media/raw.ceu"
#include "env-events.ceu"

/* internal */
event (void) mirror_ok;

#define KEY 0
#define CLICK 1

par/or do
  #include "mars-client.ceu"

  var& IPeer peer;
  watching Peer (LOCALHOST, 8888, 0) -> (&peer)
  do
    par do
      var uint id;
      var usize peers;
      (id, peers) = await peer.joined;
      emit mirror_ok;
      emit id_ready (id);
    with
      loop do
        var uint new_peer;
        var usize peers;
        (new_peer, peers) = await peer.new_peer;
      end
    with
      every peer.incoming_message do
        _printf ("[APP(%u)] Message: %s\n", peer.id, &&peer.message[0]);

        [[ TABLE = load (@peer.message)() ]]
        var int type = [[ TABLE.type ]];

        if type == KEY then
          vector[] byte key = [] .. [[ TABLE.key ]];
          emit keyed (&&key[0], peer.id);

        else/if type == CLICK then
          var uint peer = [[ TABLE.id ]];
          emit clicked (peer);
        end
      end
    with
      /* KEY */
      loop do
        var _char_ptr_ext key = await send_key;
        peer.wbuffer = [] .. "{";
        peer.wbuffer = peer.wbuffer .. "id=" .. [[ tostring(@peer.id) ]];
        peer.wbuffer = peer.wbuffer .. ",type=" .. [[ tostring(@KEY) ]];
        peer.wbuffer = peer.wbuffer .. ",key=\'" .. (_ID(key) as _char&&) .. "'";
        peer.wbuffer = peer.wbuffer .. "}";

        emit peer.send;
        await peer.sent;
      end
    with
      /* CLICK */
      loop do
        await send_click;
        peer.wbuffer = [] .. "{";
        peer.wbuffer = peer.wbuffer .. "id=" .. [[ tostring(@peer.id) ]];
        peer.wbuffer = peer.wbuffer .. ",type=" .. [[ tostring(@CLICK) ]];
        peer.wbuffer = peer.wbuffer .. "}";

        emit peer.send;
        await peer.sent;
      end
    end
  end
with
  await mirror_ok;
  #include PROG
end

escape 0;
