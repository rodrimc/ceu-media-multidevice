#include "mars-media.ceu"

/* internal */
event (void) mirror_ok;

#define KEY 0
#define CLICK 1

par/or do
  #include "mars-client.ceu"

  pool [] Stub_Send_Message send_message_pool;

  var& IStub stub;
  watching Stub (LOCALHOST, 8888, 0) -> (&stub)
  do
    par do
      var uint id;
      var usize peers;
      (id, peers) = await stub.joined;
      my_id = id;
    with
      watching mirror_ok do
        var uint new_peer;
        var usize peers;
        every (new_peer, peers) in stub.new_peer do
          if (peers >= 2) then
            emit mirror_ok;
          end
        end
      end
    with
      loop do
        await stub.incoming_message;
        /* _printf ("[APP(%u)] Message: %s\n", stub.id, &&stub.message[0]); */

        [[ TABLE = load (@stub.message)() ]]
        var int type = [[ TABLE.type ]];
        var uint peer = [[ TABLE.id ]];

        if type == KEY then
          vector[] byte key = [] .. [[ TABLE.key ]];
          var bool press = [[ TABLE.press ]];

          emit remote_mars_scene_key (&&key[0], press, peer);
        else/if type == CLICK then
          var int x = [[ TABLE.x ]];
          var int y = [[ TABLE.y ]];
          var int button = [[ TABLE.button ]];
          var bool press = [[ TABLE.press ]];

          emit remote_mars_scene_mouse_clicked (x, y, button, press, peer);
        end
      end
    with
      /* KEY */
      var _char_ptr_ext key;
      var bool press;
      every (key, press) in mars_scene_key do
        stub.wbuffer = [] .. "{";
        stub.wbuffer = stub.wbuffer .. "id=" .. [[ tostring(@stub.id) ]];
        stub.wbuffer = stub.wbuffer .. ",type=" .. [[ tostring(@KEY) ]];
        stub.wbuffer = stub.wbuffer .. ",key=\'" .. (_ID(key) as _char&&) .. "'";
        stub.wbuffer = stub.wbuffer .. ",press=" .. [[ tostring(@press) ]];
        stub.wbuffer = stub.wbuffer .. "}";

        spawn Stub_Send_Message (&stub) in send_message_pool;
      end
    with
      /* CLICK */
      var int x;
      var int y;
      var int button;
      var bool press;
      every (x, y, button, press) in mars_scene_mouse_clicked do
        stub.wbuffer = [] .. "{";
        stub.wbuffer = stub.wbuffer .. "id=" .. [[ tostring(@stub.id) ]];
        stub.wbuffer = stub.wbuffer .. ",type=" .. [[ tostring(@CLICK) ]];
        stub.wbuffer = stub.wbuffer .. ",x=" .. [[ tostring(@x) ]];
        stub.wbuffer = stub.wbuffer .. ",y=" .. [[ tostring(@y) ]];
        stub.wbuffer = stub.wbuffer .. ",button=" .. [[ tostring(@button) ]];
        stub.wbuffer = stub.wbuffer .. ",press=" .. [[ tostring(@press) ]];
        stub.wbuffer = stub.wbuffer .. "}";

        spawn Stub_Send_Message (&stub) in send_message_pool;
      end
    end
  end
with
  await mirror_ok;
  #include PROG
end

escape 0;
